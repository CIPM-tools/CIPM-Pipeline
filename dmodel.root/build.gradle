buildscript { 
    repositories {
        jcenter()
		mavenCentral()
		maven {
		  url "https://plugins.gradle.org/m2/"
		}
    }
}

// checking the presence of an Eclipse API baseline repositorsy
if (project.properties['api.baseline'] == null || project.properties['api.baseline'].isEmpty()) {
	System.err.println("Cannot find an Eclipse API baseline repository")
} else {	
	println "Baseline repository is at: " + project.rootDir + project.properties['api.baseline']
}

def listConfigurationDependencies(Configuration configuration) {
    def formatStr = "%,10.2f"

    def size = configuration.collect { it.length() / (1024 * 1024) }.sum()

    def out = new StringBuffer()
    out << "\nConfiguration name: \"${configuration.name}\"\n"
    if (size) {
        out << 'Total dependencies size:'.padRight(65)
        out << "${String.format(formatStr, size)} Mb\n\n"

        configuration.sort { -it.length() }
                .each {
                    out << "${it.name}".padRight(65)
                    out << "${String.format(formatStr, (it.length() / 1024))} kb\n"
                }
    } else {
        out << 'No dependencies found';
    }
    println(out)
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'eclipse'
    
    /* Tested with Gradle 6.3 */

	tasks.register("depsize") {
    	description = 'Prints dependencies for "default" configuration'
    	doLast() {
    	   listConfigurationDependencies(configurations.default)
   		}
	}

	tasks.register("depsize-all-configurations") {
    	description = 'Prints dependencies for all available configurations'
    	doLast() {
        	configurations.each {
	    	if (it.isCanBeResolved()) {
	       		listConfigurationDependencies(it)
            	}
        	}
    	}
	}
    
    sourceSets {
		main{
        	java {
            	srcDir 'src/main/java'
            	srcDir 'src-gen/java'
        	}
        	resources {
        		srcDirs= ["src/main/resources"]
        		exclude '**/*.xcore'
      			exclude '**/*.ecore'
      			exclude '**/*.genmodel'
        	}
    	}
    	test {
        	java {
            	srcDirs = ['src/test/java', 'src/testFixtures/java']
        	}
    	}
	}
	
	dependencies {
		// lombok
		compileOnly 'org.projectlombok:lombok:1.18.12'
		annotationProcessor 'org.projectlombok:lombok:1.18.12'
		testCompileOnly 'org.projectlombok:lombok:1.18.12'
		testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
	
		// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
		implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'
		
		// https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-yaml
		implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.8'
		
		// https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
		implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
		
		// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-web
		implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.2.0.RELEASE'
		
		// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-thymeleaf
		implementation group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf', version: '2.2.0.RELEASE'
		
		implementation group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.3'
		
		// https://mvnrepository.com/artifact/nz.net.ultraq.thymeleaf/thymeleaf-layout-dialect
		implementation group: 'nz.net.ultraq.thymeleaf', name: 'thymeleaf-layout-dialect', version: '2.4.1'
		
		// https://mvnrepository.com/artifact/junit/junit
		testImplementation group: 'junit', name: 'junit', version: '4.12'
		
		testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.1.6.RELEASE'
		
		/* Test Fixtures */
		testFixturesImplementation group: 'junit', name: 'junit', version: '4.12'
		
		testFixturesImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.2.0.RELEASE'
		testFixturesImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.1.6.RELEASE'
		testFixturesImplementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.8'
		testFixturesImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'
	}

    repositories {
		maven {
			url uri(project.rootDir.toString() + project.properties['api.baseline'])
			metadataSources {
            	artifact()
        	}
		}
		maven {url 'https://repo.typesafe.com/typesafe/maven-releases/'}
		mavenCentral()
		jcenter()
    }
}