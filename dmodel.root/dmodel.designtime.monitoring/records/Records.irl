package dmodel.designtime.monitoring.records

// overall type for all of our records
template PCMContextRecord {
}

template RecordWithSession : PCMContextRecord {
   string sessionId = "<not set>"
}

template HostContextRecord : PCMContextRecord {
	string hostId = "<not set>"
	string hostName = "<not set>"
}

template ServiceContextRecord : RecordWithSession {
	string serviceExecutionId = "<not set>"
}

entity ResourceUtilizationRecord : HostContextRecord {
   string resourceId = "<not set>"
   double utilization
   long timestamp
}

entity BranchRecord : ServiceContextRecord {
   string executedBranchId = "<not set>"
}

entity LoopRecord : ServiceContextRecord {
   string loopId = "<not set>"
   long loopIterationCount
}

entity ResponseTimeRecord : ServiceContextRecord {
   string internalActionId = "<not set>"
   string resourceId = "<not set>"
   long startTime
   long stopTime
}

entity ServiceCallRecord : ServiceContextRecord, HostContextRecord {
   string serviceId = "<not set>"
   string parameters = "<not set>"
   string callerServiceExecutionId = "<not set>"
   string externalCallId = "<not set>"
   string executionContextId = "<not set>"
   long entryTime
   long exitTime
}