
import "http://palladiosimulator.org/PalladioComponentModel/ResourceEnvironment/5.2" as ResourceEnvironment
import "http://www.dmodel.com/RuntimeEnvironmentModel" as REM

reactions: pcmToREM
in reaction to changes in PCMExtended
execute actions in REM

reaction DeleteResourceContainerCorrespondence {
	after element ResourceEnvironment::ResourceContainer deleted
	call deleteCorrespondenceContainer(affectedEObject)
}

reaction DeleteLinkingResourceCorrespondence {
	after element ResourceEnvironment::LinkingResource deleted
	call deleteCorrespondenceLink(affectedEObject)
}

// ROUTINES
routine deleteCorrespondenceContainer(ResourceEnvironment::ResourceContainer container) {
	match {
		val corr = retrieve REM::RuntimeResourceContainer corresponding to container
		val parent = retrieve REM::RuntimeEnvironmentModel corresponding to container.eContainer
	}
	action {
		remove correspondence between container and corr
		call {
			parent.containers -= corr
		}
	}
}

routine deleteCorrespondenceLink(ResourceEnvironment::LinkingResource link) {
	match {
		val corr = retrieve REM::RuntimeResourceContainerConnection corresponding to link
		val parent = retrieve REM::RuntimeEnvironmentModel corresponding to link.eContainer
	}
	action {
		remove correspondence between link and corr
		call {
			parent.connections -= corr
		}
	}
}
